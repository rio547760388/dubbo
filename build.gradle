buildscript {
    ext {
        springBootVersion = '2.2.2.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        /* 添加插件依赖路径（通过jar方式） */
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}" as Object)
    }
}

subprojects {
    apply(plugin: 'java')
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.tian'
    version = '1.0.0'

    sourceCompatibility = '1.8'

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    repositories {
        mavenLocal()
        maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
        mavenCentral()
    }

    dependencies {

        //springboot core
        implementation('org.springframework.boot:spring-boot-starter')

        implementation('org.apache.dubbo:dubbo-spring-boot-starter:2.7.6')

        // This dependency is used by the application.
        implementation 'com.google.guava:guava:28.0-jre'
        implementation('org.apache.dubbo:dubbo:2.7.6')

        implementation('org.apache.zookeeper:zookeeper:3.4.10') {
            exclude group: 'log4j'
            exclude group: 'org.slf4j'
        }

        // https://mvnrepository.com/artifact/org.apache.curator/curator-recipes
        implementation('org.apache.curator:curator-recipes:4.3.0') {
            exclude group: 'org.apache.zookeeper', module: 'zookeeper'
        }

        // https://mvnrepository.com/artifact/org.apache.curator/curator-framework
        implementation('org.apache.curator:curator-framework:4.3.0') {
            exclude group: 'org.apache.zookeeper', module: 'zookeeper'
        }

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }

    }

    test {
        useJUnitPlatform()
    }

    javadoc.options {
        use = true
        author = true
        version = true
    }
}







